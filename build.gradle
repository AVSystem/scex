task wrapper(type: Wrapper) {
    gradleVersion = "1.6"
}

apply plugin: 'maven'

allprojects {
    apply plugin: 'java'
    apply plugin: 'idea'

    group = "com.avsystem"
    version = '0.9'

    repositories {
        mavenCentral()
        mavenLocal()
    }

    sourceCompatibility = 1.7
    targetCompatibility = 1.7

    ext.scalaVersion = '2.10.1'

    idea {
        module {
            downloadJavadoc = true
            downloadSources = true
        }
    }
}

jar {
    def projs = [project(":scex-core"), project(":scex-derived")]
    dependsOn(projs.assemble)

    projs.each { proj ->
        from proj.configurations.archives.allArtifacts.files.collect {
            zipTree(it)
        }
    }
}

project("scex-core") {
    apply plugin: 'scala'

    dependencies {
        compile("org.scala-lang:scala-library:$scalaVersion")
        compile("org.scala-lang:scala-reflect:$scalaVersion")
        compile("org.scala-lang:scala-compiler:$scalaVersion")
        compile("org.scala-lang:jline:$scalaVersion")

        compile("commons-io:commons-io:2.1")
        compile("commons-lang:commons-lang:2.6")
        compile("commons-net:commons-net:3.0.1")
        compile("commons-pool:commons-pool:1.6")
        compile("commons-cli:commons-cli:1.2")
        compile("commons-codec:commons-codec:1.7")

        compile("com.google.guava:guava:13.0.1")
        compile("com.google.code.findbugs:jsr305:2.0.1")

        testCompile("org.scalatest:scalatest_2.10:1.9.1")
    }
}

project("scex-derived") {
    apply plugin: 'scala'

    dependencies {
        compile("org.scala-lang:scala-library:$scalaVersion")

        compile(project(":scex-core"))

        testCompile("org.scalatest:scalatest_2.10:1.9.1")
    }
}

project("scex-test") {
    apply plugin: 'scala'

    dependencies {
        compile "org.scala-lang:scala-library:$scalaVersion"

        compile(project(":scex-core"))
        compile(project(":scex-derived"))

        testCompile("org.scalatest:scalatest_2.10:1.9.1")
    }
}

project("scex-java-test") {
    dependencies {
        compile(project(":scex-core"))
        compile(project(":scex-derived"))
    }
}
